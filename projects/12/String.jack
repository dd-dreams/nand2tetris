// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/String.jack

/**
 * Represents character strings. In addition for constructing and disposing
 * strings, the class features methods for getting and setting individual
 * characters of the string, for erasing the string's last character,
 * for appending a character to the string's end, and more typical
 * string-oriented operations.
 */
class String {
    field Array str;
    field int length, max_length;

    /** constructs a new empty string with a maximum length of maxLength
     *  and initial length of 0. */
    constructor String new(int maxLength) {
        if (maxLength = 0) {
            let maxLength = 1;
        }
        let max_length = maxLength;
        let str = Array.new(maxLength);
        let length = 0;
        return this;
    }

    /** Disposes this string. */
    method void dispose() {
        do str.dispose();
        do Memory.deAlloc(this);
        return;
    }

    /** Returns the current length of this string. */
    method int length() {
        return length;
    }

    /** Returns the character at the j-th location of this string. */
    method char charAt(int j) {
        return str[j];
    }

    /** Sets the character at the j-th location of this string to c. */
    method void setCharAt(int j, char c) {
        let str[j] = c;
        return;
    }

    /** Appends c to this string's end and returns this string. */
    method String appendChar(char c) {
        if (length < max_length) {
            let str[length] = c;
            let length = length + 1;
        }
        return this;
    }

    /** Erases the last character from this string. */
    method void eraseLastChar() {
        let length = length - 1;
        let str[length] = 0;
        return;
    }
    
    /* returns if str[j] is a digit */
    method boolean isdigit(int j) {
        if ((str[j] < 48) | (str[j] > 57)) {
            return false;
        }
        return true;

    }

    method int string2Int(String val, int i) {
        var int final;
        let final = 0;

        while (i < length) {
            let final = (final * 10) + (val[i] - 48);
            let i = i + 1;
        }
        return final;
    }

    method void int2String(int val) {
        var int lastDigit;

        let lastDigit = val / 10;
        if (val < 10) {
            do appendChar(val + 48);
        }
        else {
            do int2String(lastDigit);
            do appendChar((val - (lastDigit * 10)) + 48);
        }
        return;
    }

    /** Returns the integer value of this string, 
     *  until a non-digit character is detected. */
    method int intValue() {
        if (length = 0) {
            return 0;
        }

        if (str[0] = 45) { // if the number is negative
            return -(string2Int(str, 1));
        }
        else {
            return string2Int(str, 0);
        }
    }

    /** Sets this string to hold a representation of the given value. */
    method void setInt(int val) {
        let length = 0;
        if (val < 0) {
            let val = -val;
            do appendChar(45); // ascii(45) = "-"
        }
        do int2String(val);
        return;
    }

    /** Returns the new line character. */
    function char newLine() {
        return 128;
    }

    /** Returns the backspace character. */
    function char backSpace() {
        return 129;
    }

    /** Returns the double quote (") character. */
    function char doubleQuote() {
        return 34;
    }
}
